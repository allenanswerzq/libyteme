# See absl/copts/copts.py and absl/copts/generate_copts.py
include(GENERATED_BytemeCopts)

set(BYTE_LSAN_LINKOPTS "")
set(BYTE_HAVE_LSAN OFF)
set(BYTE_DEFAULT_LINKOPTS "")

if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
  set(BYTE_DEFAULT_COPTS "${BYTE_GCC_FLAGS}")
  set(BYTE_TEST_COPTS "${BYTE_GCC_FLAGS};${BYTE_GCC_TEST_FLAGS}")
  set(BYTE_RANDOM_RANDEN_COPTS "${BYTE_RANDOM_HWAES_X64_FLAGS}")
elseif("${CMAKE_CXX_COMPILER_ID}" MATCHES "Clang")
  # MATCHES so we get both Clang and AppleClang
  if(MSVC)
    # clang-cl is half MSVC, half LLVM
    set(BYTE_DEFAULT_COPTS "${BYTE_CLANG_CL_FLAGS}")
    set(BYTE_TEST_COPTS "${BYTE_CLANG_CL_FLAGS};${BYTE_CLANG_CL_TEST_FLAGS}")
    set(BYTE_DEFAULT_LINKOPTS "${BYTE_MSVC_LINKOPTS}")
    set(BYTE_RANDOM_RANDEN_COPTS "${BYTE_RANDOM_HWAES_MSVC_X64_FLAGS}")
  else()
    set(BYTE_DEFAULT_COPTS "${BYTE_LLVM_FLAGS}")
    set(BYTE_TEST_COPTS "${BYTE_LLVM_FLAGS};${BYTE_LLVM_TEST_FLAGS}")
    set(BYTE_RANDOM_RANDEN_COPTS "${BYTE_RANDOM_HWAES_X64_FLAGS}")
    if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
      # AppleClang doesn't have lsan
      # https://developer.apple.com/documentation/code_diagnostics
      if(NOT CMAKE_CXX_COMPILER_VERSION VERSION_LESS 3.5)
        set(BYTE_LSAN_LINKOPTS "-fsanitize=leak")
        set(BYTE_HAVE_LSAN ON)
      endif()
    endif()
  endif()
elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")
  set(BYTE_DEFAULT_COPTS "${BYTE_MSVC_FLAGS}")
  set(BYTE_TEST_COPTS "${BYTE_MSVC_FLAGS};${BYTE_MSVC_TEST_FLAGS}")
  set(BYTE_DEFAULT_LINKOPTS "${BYTE_MSVC_LINKOPTS}")
  set(BYTE_RANDOM_RANDEN_COPTS "${BYTE_RANDOM_HWAES_MSVC_X64_FLAGS}")
else()
  message(WARNING "Unknown compiler: ${CMAKE_CXX_COMPILER}.  Building with no default flags")
  set(BYTE_DEFAULT_COPTS "")
  set(BYTE_TEST_COPTS "")
  set(BYTE_RANDOM_RANDEN_COPTS "")
endif()

if("${CMAKE_CXX_STANDARD}" EQUAL 98)
  message(FATAL_ERROR "Abseil requires at least C++11")
elseif(NOT "${CMAKE_CXX_STANDARD}")
  message(STATUS "No CMAKE_CXX_STANDARD set, assuming 11")
  set(BYTE_CXX_STANDARD 11)
else()
  set(BYTE_CXX_STANDARD "${CMAKE_CXX_STANDARD}")
endif()
